import org.jetbrains.kotlin.konan.target.HostManager

plugins {
  alias(deps.plugins.kotlin.jvm)
  alias(deps.plugins.shadow) apply false
  alias(deps.plugins.publish)
  alias(deps.plugins.dokka)
  id("java-gradle-plugin")
  id("jvm-test-suite")
}

testing {
  suites {
    dockerTest(JvmTestSuite) {
      useJUnit()
    }
  }
}

configurations.dockerTestImplementation.extendsFrom(configurations.testImplementation)

gradlePlugin {
  plugins {
    sqlDelight {
      id = 'app.cash.sqldelight'
      implementationClass = 'app.cash.sqldelight.gradle.SqlDelightPlugin'
    }
  }

  testSourceSets(sourceSets.test, sourceSets.dockerTest)
}

configurations {
  bundled
  shade
}

dependencies {
  api(deps.sqlPsi) {
    exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib-jdk8'
  }

  implementation deps.sqliteJdbc
  implementation deps.objectDiff
  implementation deps.schemaCrawler.tools
  implementation deps.schemaCrawler.sqlite

  implementation project(':sqlite-migrations')
  implementation project(':sqldelight-compiler')
  shade deps.intellij.analysis
  shade deps.intellij.core
  shade deps.intellij.java
  shade deps.intellij.lang
  shade deps.intellij.testFramework

  compileOnly gradleApi()
  implementation deps.kotlin.plugin
  compileOnly deps.android.plugin

  testImplementation deps.sqlPsi
  testImplementation project(':sqlite-migrations')
  testImplementation project(':sqldelight-compiler')
  testImplementation deps.junit
  testImplementation deps.truth
  testImplementation deps.testParameterInjector
}

test {
  // The integration tests require local installations of artifacts.
  dependsOn(
      ":dialects:mysql:publishAllPublicationsToInstallLocallyRepository",
      ":dialects:hsql:publishAllPublicationsToInstallLocallyRepository",
      ":dialects:postgresql:publishAllPublicationsToInstallLocallyRepository",
      ":dialects:sqlite:json-module:publishAllPublicationsToInstallLocallyRepository",
      ":dialects:sqlite-3-18:publishAllPublicationsToInstallLocallyRepository",
      ":dialects:sqlite-3-24:publishAllPublicationsToInstallLocallyRepository",
      ":dialects:sqlite-3-25:publishAllPublicationsToInstallLocallyRepository",
      ":dialects:sqlite-3-30:publishAllPublicationsToInstallLocallyRepository",
      ":dialects:sqlite-3-35:publishAllPublicationsToInstallLocallyRepository",
      ":dialects:sqlite-3-38:publishAllPublicationsToInstallLocallyRepository",
      ":sqldelight-compiler:dialect:publishAllPublicationsToInstallLocallyRepository",
      ":runtime:publishKotlinMultiplatformPublicationToInstallLocallyRepository",
      ":runtime:publishJvmPublicationToInstallLocallyRepository",
      ":runtime:publishJsPublicationToInstallLocallyRepository",
      ":drivers:android-driver:publishAllPublicationsToInstallLocallyRepository",
      ":drivers:sqlite-driver:publishAllPublicationsToInstallLocallyRepository",
      ":drivers:jdbc-driver:publishAllPublicationsToInstallLocallyRepository",
      ":sqlite-migrations:publishAllPublicationsToInstallLocallyRepository",
      ":sqldelight-compiler:publishAllPublicationsToInstallLocallyRepository",
      ":sqldelight-gradle-plugin:publishAllPublicationsToInstallLocallyRepository",
  )
  if (HostManager.hostIsMac) {
    dependsOn(
        ":drivers:native-driver:publishIosX64PublicationToInstallLocallyRepository",
        ":drivers:native-driver:publishIosSimulatorArm64PublicationToInstallLocallyRepository",
        ":drivers:native-driver:publishKotlinMultiplatformPublicationToInstallLocallyRepository",
        ":runtime:publishIosX64PublicationToInstallLocallyRepository",
        ":runtime:publishIosSimulatorArm64PublicationToInstallLocallyRepository",
        ":runtime:publishIosArm64PublicationToInstallLocallyRepository",
    )
  }
  useJUnit {
    if (project.hasProperty("Instrumentation")) {
      includeCategories 'app.cash.sqldelight.Instrumentation'
    } else {
      excludeCategories 'app.cash.sqldelight.Instrumentation'
    }
  }
}

tasks.named('check') {
  dependsOn(testing.suites.dockerTest)
}

tasks.dockerTest.configure {
  if (project.hasProperty("Instrumentation")) enabled = false
  dependsOn(
      ":dialects:mysql:publishAllPublicationsToInstallLocallyRepository",
      ":dialects:postgresql:publishAllPublicationsToInstallLocallyRepository",
      ":sqldelight-compiler:dialect:publishAllPublicationsToInstallLocallyRepository",
      ":runtime:publishKotlinMultiplatformPublicationToInstallLocallyRepository",
      ":runtime:publishJvmPublicationToInstallLocallyRepository",
      ":drivers:jdbc-driver:publishAllPublicationsToInstallLocallyRepository",
      ":sqlite-migrations:publishAllPublicationsToInstallLocallyRepository",
      ":sqldelight-compiler:publishAllPublicationsToInstallLocallyRepository",
      ":sqldelight-gradle-plugin:publishAllPublicationsToInstallLocallyRepository",
  )
}

validatePlugins {
  enableStricterValidation = true
}

if (true) {
  apply plugin: 'com.github.johnrengelman.shadow'

  configurations.compileOnly.extendsFrom(configurations.shade)

  tasks.getByName("shadowJar").configure {
    archiveClassifier.set("")
    minimize()
    configurations = [project.configurations.shade]

    include '*.jar'
    include '**/*.class'
    include 'META-INF/gradle-plugins/*'
    include 'messages/*.properties'

    exclude '/groovy**'
    exclude '/kotlin/**'
  }

  artifacts {
    runtimeOnly(shadowJar)
    archives(shadowJar)
  }
} else {
  configurations.implementation.extendsFrom(configurations.shade)
}

apply from: "$rootDir/gradle/gradle-mvn-push.gradle"
